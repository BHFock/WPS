#!/bin/csh -f

if ( ! -e configure.wps ) then
	echo "Do 'configure' first"
	exit ( 1 ) 
endif

if  ( ( ! $?NETCDF ) && ( -d netcdf_links ) ) then
	setenv NETCDF `pwd`/netcdf_links
	setenv temp_netcdf 1
else
	setenv temp_netcdf 0
endif

echo " "
echo "**** Compiling wps"
echo " "

set DEV_TOP = $PWD
set first_char = `grep ^WRF_DIR configure.wps | awk '{print $3}' | cut -c -1`

if ( "$first_char" == "/" ) then
	set WRF_DIR_PRE	=
else
	set WRF_DIR_PRE	=	${DEV_TOP}/
endif

if      ( ( ${#argv} == 0 ) || ( $1 == util ) ) then
	set names	=	( geogrid ungrib metgrid )
	set NAMES	=	( GEOGRID UNGRIB METGRID )
else if ( $1 == geogrid ) then
	set names	=	( geogrid )
	set NAMES	=	( GEOGRID )
else if ( $1 == ungrib  ) then
	set names	=	( ungrib  )
	set NAMES	=	( UNGRIB  )
else if ( $1 == metgrid ) then
	set names	=	( metgrid )
	set NAMES	=	( METGRID )
else if ( $1 == util    ) then
	set names	=
endif

set count = 1
foreach f ( $names )
	if ( $f == ungrib ) then
		( cd ungrib ; make -i -r DEV_TOP="${DEV_TOP}" all )
		if ( -e ${f}/src/${f}.exe ) then
			ln -sf ${f}/src/${f}.exe .
		endif
	else
		( cd $f ; make -i -r WRF_DIR_PRE="${WRF_DIR_PRE}" DEV_TOP="${DEV_TOP}" TARGET="${f}.exe" CPP_TARGET="$NAMES[$count]" all )
		if ( -e ${f}/src/${f}.exe ) then
			ln -sf ${f}/src/${f}.exe .
		endif
	endif
	@ count ++
end

if ( $1 == util ) then
	if ( -e geogrid/src/plotgrids.exe ) then
		ln -sf geogrid/src/plotgrids.exe .
	endif
	
	if ( -e ungrib/src/g1print.exe ) then
		ln -sf ungrib/src/g1print.exe .
	endif
	
	if ( -e ungrib/src/g2print.exe ) then
		ln -sf ungrib/src/g2print.exe .
	endif
	
	if ( -e ungrib/src/plotfmt.exe ) then
		ln -sf ungrib/src/plotfmt.exe .
	endif
	
	if ( -e ungrib/src/mod_levs.exe ) then
		ln -sf ungrib/src/mod_levs.exe .
	endif
	
	if ( -e ungrib/src/rd_intermediate.exe ) then
		ln -sf ungrib/src/rd_intermediate.exe .
	endif
endif

if  ( $temp_netcdf == 1 ) then
	unsetenv NETCDF
endif
